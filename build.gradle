// Top-level build file where you can add configuration options common to all sub-projects/modules.
import com.dpal.Dependencies
import com.dpal.Config

buildscript {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath Dependencies.gradle
        classpath Dependencies.Kotlin.gradle
        classpath Dependencies.Klint.gradle
        classpath 'com.squareup.sqldelight:gradle-plugin:1.4.4'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.32'
    }
}

apply plugin: 'kotlin'
apply plugin: Dependencies.Klint.plugin
apply plugin: 'com.squareup.sqldelight'

subprojects {

    apply plugin: Dependencies.Klint.plugin

    ktlint {
        debug = true
    }

    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion Config.compileSdk
                buildToolsVersion Config.buildTools

                defaultConfig {
                    minSdkVersion Config.minSdk
                    targetSdkVersion Config.targetSdk

                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }
                buildFeatures {
                    viewBinding true
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
                kotlinOptions {
                    jvmTarget = '1.8'
                }

                composeOptions {
                    kotlinCompilerExtensionVersion Dependencies.Compose.version
                }
            }
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}