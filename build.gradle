// Top-level build file where you can add configuration options common to all sub-projects/modules.
import com.dpal.Dependencies
import com.dpal.Config

buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath Dependencies.gradle
        classpath Dependencies.Kotlin.gradle
        classpath 'com.squareup.sqldelight:gradle-plugin:1.4.4'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.32'
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.16.0"
    }
}

//detekt {
//
//}

apply plugin: 'kotlin'
apply plugin: 'com.squareup.sqldelight'

apply from: "detekt.gradle"

task detektProjectBaseline(type: io.gitlab.arturbosch.detekt.DetektCreateBaselineTask) {
    description = "Overrides current baseline."
    ignoreFailures.set(true)
    parallel.set(true)
    buildUponDefaultConfig.set(true)
    setSource(files(rootDir))
    config.setFrom(files("$rootDir/config/detekt/config.yml"))
    baseline.set(file("$rootDir/config/detekt/baseline.xml"))
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/resources/**")
    exclude("**/build/**")
}

subprojects {
    apply from: "$rootDir/detekt.gradle"
    afterEvaluate { project ->
        if (project.hasProperty('android')) {

            android {
                compileSdkVersion Config.compileSdk
                buildToolsVersion Config.buildTools

                defaultConfig {
                    minSdkVersion Config.minSdk
                    targetSdkVersion Config.targetSdk

                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }
                buildFeatures {
                    viewBinding true
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
                kotlinOptions {
                    jvmTarget = '1.8'
                }

                composeOptions {
                    kotlinCompilerExtensionVersion Dependencies.Compose.version
                }
            }
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}